
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = 'org.whitesource'
version = '0.10'

buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.whitesource:wss-agent-api-client:2.3.8', 'org.whitesource:wss-agent-report:2.3.8'
    compile 'org.codehaus.groovy:groovy-backports-compat23:2.4.11'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'WhiteSource Gradle Plugin'
                packaging 'jar'
                description 'Gradle plugin to send open source software usage updates to WhiteSource.'
                url 'https://github.com/whitesource/gradle-plugin'

                scm {
                    connection 'scm:git:git@github.com:whitesource/gradle-plugin.git'
                    developerConnection 'scm:git:git@github.com:whitesource/gradle-plugin.git'
                    url 'https://github.com/whitesource/gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'itai.marko'
                        name 'Itai.Marko'
                        email 'itai.marko@whitesourcesoftware.com'
                    }
                    developer {
                        id 'alex.maybaum'
                        name 'Alex.Maybaum'
                        email 'alex.maybaum@whitesourcesoftware.com'
                    }
                }
            }
        }
    }
}
